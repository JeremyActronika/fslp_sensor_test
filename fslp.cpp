#include <Arduino.h>
#include "fslp.hpp"

// This function follows the steps described in the FSLP
// integration guide to measure the position of a force on the
// sensor.  The return value of this function is proportional to
// the physical distance from drive line 2, and it is between
// 0 and 1023.  This function does not give meaningful results
// if fslpGetPressure is returning 0.
int fslpGetPosition()
{
  // Step 1 - Clear the charge on the sensor.
  pinMode(SENSE_LINE, OUTPUT);
  digitalWrite(SENSE_LINE, LOW);

  pinMode(DRIVE_LINE_1, OUTPUT);
  digitalWrite(DRIVE_LINE_1, LOW);

  pinMode(DRIVE_LINE_2, OUTPUT);
  digitalWrite(DRIVE_LINE_2, LOW);

  pinMode(BOT_R0, OUTPUT);
  digitalWrite(BOT_R0, LOW);

  // Step 2 - Set up appropriate drive line voltages.
  digitalWrite(DRIVE_LINE_1, HIGH);
  pinMode(BOT_R0, INPUT);
  pinMode(SENSE_LINE, INPUT);

  // Step 3 - Wait for the voltage to stabilize.
  delayMicroseconds(10);

  // Step 4 - Take the measurement.
  analogReset();
  return analogRead(SENSE_LINE);
}

// This function follows the steps described in the FSLP
// integration guide to measure the pressure on the sensor.
// The value returned is usually between 0 (no pressure)
// and 500 (very high pressure), but could be as high as
// 32736.
int fslpGetPressure()
{
  // Step 1 - Set up the appropriate drive line voltages.
  pinMode(DRIVE_LINE_1, OUTPUT);
  digitalWrite(DRIVE_LINE_1, HIGH);

  pinMode(BOT_R0, OUTPUT);
  digitalWrite(BOT_R0, LOW);

  pinMode(SENSE_LINE, INPUT);

  pinMode(DRIVE_LINE_2, INPUT);

  // Step 2 - Wait for the voltage to stabilize.
  delayMicroseconds(10);

  // Step 3 - Take two measurements.
  analogReset();
  int v1 = analogRead(DRIVE_LINE_2);
  analogReset();
  int v2 = analogRead(SENSE_LINE);

  // Step 4 - Calculate the pressure.
  // Detailed information about this formula can be found in the
  // FSLP Integration Guide.
  if (v1 == v2)
  {
    // Avoid dividing by zero, and return maximum reading.
    return 32 * 1023;
  }
  return 32 * v2 / (v1 - v2);
}

// Performs an ADC reading on the internal GND channel in order
// to clear any voltage that might be leftover on the ADC.
// Only works on AVR boards and silently fails on others.
void analogReset()
{
#if defined(ADMUX)
#if defined(ADCSRB) && defined(MUX5)
    // Code for the ATmega2560 and ATmega32U4
    ADCSRB |= (1 << MUX5);
#endif
    ADMUX = 0x1F;

    // Start the conversion and wait for it to finish.
    ADCSRA |= (1 << ADSC);
    loop_until_bit_is_clear(ADCSRA, ADSC);
#endif
}
